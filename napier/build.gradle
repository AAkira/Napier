apply plugin: 'kotlin-multiplatform'
apply from: 'android.gradle'

apply from: rootProject.file('gradle/publish.gradle')

kotlin {
    android {
        publishLibraryVariants("release")
    }
    iosX64 {
        binaries {
            framework()
        }
    }
    iosArm32 {
        binaries {
            framework()
        }
    }
    iosArm64 {
        binaries {
            framework()
        }
    }
    js()
    jvm()

    sourceSets {
        commonMain {
            dependencies {
                implementation rootProject.ext.kotlinCommon
            }
        }
        commonTest {
            dependencies {
                implementation rootProject.ext.coroutineCommon

                implementation rootProject.ext.kotlinTest
                implementation rootProject.ext.kotlinTestAnnotation
            }
        }
        androidMain {
            dependencies {
            }
        }
        androidTest {
            dependencies {
                implementation rootProject.ext.kotlinTestJvm
            }
        }
        iosMain {
            dependencies {
            }
        }
        iosTest {
            dependencies {
                implementation rootProject.ext.coroutineNative
            }
        }
        configure([iosX64Main, iosArm64Main, iosArm32Main]) {
            dependsOn iosMain
        }
        configure([iosX64Test, iosArm64Test, iosArm32Test]) {
            dependsOn iosTest
        }
        jsMain {
            dependencies {
                implementation rootProject.ext.kotlinJs
            }
        }
        jsTest {
            dependencies {
                implementation rootProject.ext.coroutineJs
            }
        }
        jvmMain {
            dependencies {
                implementation rootProject.ext.kotlinJvm
            }
        }
        jvmTest {
            dependencies {
                implementation rootProject.ext.coroutine

                implementation rootProject.ext.kotlinTestJvm
            }
        }
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

[compileKotlinJs, compileTestKotlinJs]*.configure {
    kotlinOptions {
        moduleKind = "umd"
    }
}
