import dependencies.Dep
import org.jetbrains.kotlin.gradle.tasks.Kotlin2JsCompile

apply plugin: 'kotlin-multiplatform'
apply from: 'android.gradle'

apply from: rootProject.file('gradle/publish.gradle')

kotlin {
    android {
        publishLibraryVariants("release")
    }
    iosX64 {
        binaries {
            framework()
        }
    }
    iosArm32 {
        binaries {
            framework()
        }
    }
    iosArm64 {
        binaries {
            framework()
        }
    }
    js() {
        browser()
        nodejs()
        compilations.all {
            tasks.withType(Kotlin2JsCompile) {
                kotlinOptions {
                    moduleKind = 'umd'
                }
            }
        }
    }
    jvm()

    sourceSets {
        commonMain {
            dependencies {
                implementation Dep.Kotlin.common
            }
        }
        commonTest {
            dependencies {
                implementation Dep.Coroutines.common

                implementation Dep.Test.common
                implementation Dep.Test.annotation
            }
        }
        androidMain {
            dependencies {
            }
        }
        androidTest {
            dependencies {
                implementation Dep.Test.jvm
            }
        }
        iosMain {
            dependencies {
            }
        }
        iosTest {
            dependencies {
                implementation Dep.Coroutines.native
            }
        }
        configure([iosX64Main, iosArm64Main, iosArm32Main]) {
            dependsOn iosMain
        }
        configure([iosX64Test, iosArm64Test, iosArm32Test]) {
            dependsOn iosTest
        }
        jsMain {
            dependencies {
                implementation Dep.Kotlin.js
            }
        }
        jsTest {
            dependencies {
                implementation Dep.Coroutines.js
                implementation Dep.Test.js
            }
        }
        jvmMain {
            dependencies {
                implementation Dep.Kotlin.jvm
            }
        }
        jvmTest {
            dependencies {
                implementation Dep.Coroutines.core

                implementation Dep.Test.jvm
            }
        }
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}
