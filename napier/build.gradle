import dependencies.Dep
import org.jetbrains.kotlin.gradle.tasks.Kotlin2JsCompile

//ordering is matter
apply plugin: 'com.android.library'
apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlin-android-extensions'

apply from: 'android.gradle'

apply from: rootProject.file('gradle/publish.gradle')

kotlin {
    android {
        publishLibraryVariants("release")
    }
    iosX64 {
        binaries {
            framework()
        }
    }
    iosArm32 {
        binaries {
            framework()
        }
    }
    iosArm64 {
        binaries {
            framework()
        }
    }
    js() {
        browser()
        nodejs()
        compilations.all {
            tasks.withType(Kotlin2JsCompile) {
                kotlinOptions {
                    moduleKind = 'umd'
                }
            }
        }
    }
    jvm()

    sourceSets {
        commonMain {
            dependencies {
                implementation Dep.Kotlin.common
            }
        }
        commonTest {
            dependencies {
                implementation Dep.Coroutines.common

                implementation Dep.Test.common
                implementation Dep.Test.annotation
            }
        }
        iosMain {
            dependsOn commonMain
        }
        iosTest {
            dependsOn commonTest
        }
        configure([iosX64Main, iosArm64Main, iosArm32Main]) {
            dependsOn iosMain
        }
        configure([iosX64Test, iosArm64Test, iosArm32Test]) {
            dependsOn iosTest
        }
    }
}
