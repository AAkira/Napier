import dependencies.Versions

apply plugin: 'maven-publish'

group = BINTRAY_PACKAGE
version = Versions.versionName

publishing.publications.all {
    pom {
        description = POM_DESCRIPTION
        url = SITE_URL
        licenses {
            license {
                name = POM_LICENSE_NAME
                url = POM_LICENSE_URL
                distribution = POM_LICENSE_DIST
            }
        }
        developers {
            developer {
                id = POM_DEVELOPER_ID
                name = POM_DEVELOPER_NAME
                organization = POM_ORGANIZATION_NAME
                organizationUrl = POM_ORGANIZATION_URL
            }
        }
        scm {
            url = SITE_URL
        }
    }
}

def getBintrayUserProperty() {
    return hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
}

def getBintrayApiKeyProperty() {
    return hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
}

publishing {
    repositories {
        maven {

            def user = 'aakira'
            def repo = 'maven'
            def name = 'napier'
            url = "https://api.bintray.com/maven/$user/$repo/$name/;publish=0;override=1"

            credentials {
                username = getBintrayUserProperty()
                password = getBintrayApiKeyProperty()
            }
        }
    }
}
