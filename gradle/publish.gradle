import dependencies.Versions
import org.gradle.api.publish.maven.internal.artifact.FileBasedMavenArtifact

apply plugin: 'maven-publish'
// todo replace
// https://kotlinlang.org/docs/reference/migrating-multiplatform-project-to-14.html#check-uploading-to-bintray
apply plugin: 'com.jfrog.bintray'

group = BINTRAY_PACKAGE
version = Versions.versionName

publishing.publications.all {
    pom {
        description = POM_DESCRIPTION
        url = SITE_URL
        licenses {
            license {
                name = POM_LICENSE_NAME
                url = POM_LICENSE_URL
                distribution = POM_LICENSE_DIST
            }
        }
        developers {
            developer {
                id = POM_DEVELOPER_ID
                name = POM_DEVELOPER_NAME
                organization = POM_ORGANIZATION_NAME
                organizationUrl = POM_ORGANIZATION_URL
            }
        }
        scm {
            url = SITE_URL
        }
    }
}

def getBintrayUserProperty() {
    return hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
}

def getBintrayApiKeyProperty() {
    return hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
}

bintray {
    user = getBintrayUserProperty()
    key = getBintrayApiKeyProperty()
    publish = false
    override = true // for multi-platform Kotlin/Native publishing

    pkg {
        repo = BINTRAY_REPOSITORY
        name = BINTRAY_NAME
        userOrg = GROUP
        licenses = ['Apache-2.0']
        vcsUrl = VCS_URL
        websiteUrl = SITE_URL
        issueTrackerUrl = ISSUE_URL

        version {
            name = Versions.versionName
            vcsTag = Versions.versionName
            released = new Date()
        }
    }
}

def isMultiplatform = project.name == "napier"

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
}

afterEvaluate {
    publishing {
        def projectName = project.name

        publications.all {
            // rename artifacts
            groupId = BINTRAY_PACKAGE

            if (it.name == 'kotlinMultiplatform') {
                // this includes all artifacts info
                it.artifactId = projectName
            } else {
                artifactId = "$projectName-${it.name}"
            }
            // disable metadata everywhere, but in ios modules
            if (!it.name.contains('ios') && it.name != 'kotlinMultiplatform') {
                moduleDescriptorGenerator = null
            }
        }
    }
}

bintrayUpload.doFirst {
    publications = project.publishing.publications.findAll { !it.name.contains('-test') }.collect {
        it.name
    }

    // https://github.com/bintray/gradle-bintray-plugin/issues/229
    project.publishing.publications.withType(MavenPublication).forEach({ publication ->
        File moduleFile = new File(project.buildDir, "publications/${publication.name}/module.json")

        if (moduleFile.exists() && publication.name != 'metadata') {
            publication.artifact(new FileBasedMavenArtifact(moduleFile) {
                protected String getDefaultExtension() {
                    return 'module';
                }
            })
        }
    })
}

bintrayUpload.dependsOn publishToMavenLocal
